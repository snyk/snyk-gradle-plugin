apply plugin: 'java'

group = 'com.github.jitpack'

sourceCompatibility = 1.8 // java 8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

// See https://docs.gradle.org/current/userguide/dependency_management_attribute_based_matching.html

if (project.hasProperty('objects')) {
  def usageAttr = Attribute.of("org.gradle.usage", Usage)
  // Gradle 4+
  configurations {
      apiConf {
          attributes {
              attribute(usageAttr, project.objects.named(Usage, "java-api"))
          }
      }
      runtimeConf {
          attributes {
              attribute(usageAttr, project.objects.named(Usage, "java-runtime"))
          }
      }
    }
} else {
 // Gradle 3
  def usageAttr = Attribute.of('usage', String)
  configurations {
      apiConf {
          attributes {
              attribute(usageAttr, "java-api")
          }
      }
      runtimeConf {
          attributes {
              attribute(usageAttr, "java-runtime")
          }
      }
  }
}

dependencies {
  implementation 'com.google.guava:guava:18.0'
  apiConf 'commons-httpclient:commons-httpclient:3.1'
  runtimeConf 'org.apache.commons:commons-lang3:3.8.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}
